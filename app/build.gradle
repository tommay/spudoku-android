apply plugin: 'com.android.application'

repositories {
   jcenter()
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    defaultConfig {
        // These go into fields in the generated  BuildConfig java class:
        applicationId "net.tommay.spudoku"
        versionCode 1
        versionName "1.0"
        buildConfigField "String", "CREATE_LOG", "null"

        minSdkVersion 15
        targetSdkVersion 25

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        // These things become gradle tasks "assembleDev", etc.
        dev {
            // Create puzzles with a consistent seed and log create times to
	    // /data/data/net.tommay.spudoku/files/create.log.
            buildConfigField "String", "CREATE_LOG", '"create.log"'
            // This minify stuff can't go in defaultConfig because the
	    // minifyEnabled method isn't available there.
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Build either the scala glue code or the frege glue code that provides
    // the java dode a uniform API to the scala/frege code.  This is the
    // only way I could figure out how to do this, by putting it in its own
    // directory tree and coditionally adding the tree to the main java
    // sourceSet.

    sourceSets {
        main {
            if (project.hasProperty("frege")) {
                java.srcDirs += "glue/frege"
            }
            else {
                java.srcDirs += "glue/scala"
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.0.0'
    // This is to get Coodinatorlayout.  It should maybe be 25.3.0
    // but gradle stalls on Preparing "Install Android Support Repository".
    compile 'com.android.support:design:23.0.1'
    testCompile 'junit:junit:4.12'
}

// The sudoku Puzzle create etc code is a separate scala or frege project.

dependencies {
    if (project.hasProperty("frege")) {
        compile project(':frege')
    }
    else {
        compile project(':scala')
    }
}
