// Useful build commands:
//   gradle installDebug
//   gradle assenbleDebug (build only, no install)
//   gradle -Pconsistent installDebug
//   gradle uninstallDebug
//   gradle installRelease

apply plugin: 'com.android.application'

// The keystore creds for signing the release apk are kept in
// keystore.properties which is not in version control.  Load
// the creds into keystoreProperties.  See
// https://developer.android.com/studio/publish/app-signing#secure-key

def keystoreProperties = new Properties()
keystoreProperties.load(
  new FileInputStream(rootProject.file("keystore.properties")))

android {
    // This has to match appcompat-v7:28 below.
    compileSdkVersion 28

    // This is no longer necessary.  Each version of the Android Gradle
    // Plugin now has a default version of the build tools.
    // buildToolsVersion "25.0.0"

    // If buildConfigField CREATE_LOG is non-null then puzzle creation times
    // will be logged to /data/data/net.tommay.spudoku/files/<CREATE_LOG>,
    // and PuzzleSupplier.java will use the same random number seed so times
    // will be consistent.

    defaultConfig {
        // These are automagically put into fields in the generated
	// BuildConfig.java and in AndroidManifest.xml.  versionCode is
        // a monotonically increasing integer for system use, versionName
        // is what users see.
        // See https://developer.android.com/studio/publish/versioning.

        applicationId "net.tommay.spudoku"
        versionCode 4
        versionName "1.0.4"

	// These are automagically put into AndroidManifest.xml.

	// minSdkVersion was 15 (Ice Cream Sandwich Android 4.0.3 - 4.0.4)
        // but I had to increase it to 24 (Nougat Android 7.0 8/2016) necause
        // that's when java.util.Arrays.Stream was added.  It's not really
        // necessary, just makes a few small functions nicer.

        minSdkVersion 24

        // targetSdkVersion is 26 because that's what's on my phone,
        // Android 8.0.

        // https://developer.android.com/distribute/best-practices/develop/target-sdk
        // Starting August 1, 2019, Google Play requires that new apps
        // target at least Android 9.0 (API level 28), and that app
        // updates target Android 9.0 from November 1, 2019.

        targetSdkVersion 26

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "CREATE_LOG", "null"
    }

    signingConfigs {
        release {
            // Use the values loaded from secure file "keystore.properties".
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        // These things become gradle tasks "assembleDev", etc.
        // The minify stuff can't go in defaultConfig because the
        // minifyEnabled method isn't available there.
        debug {
            if (project.hasProperty("consistent")) {
              // Create puzzles with a consistent seed and log create times to
              // /data/data/net.tommay.spudoku/files/create.log.
              buildConfigField "String", "CREATE_LOG", '"create.log"'
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'scala.pro'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'scala.pro'
        }
    }

    // Build the glue code to the scala project.

    sourceSets {
        main {
            java.srcDirs += "glue/scala"
        }
    }

    // By default I think the compatibilities are 1.7 but we need
    // 1.8 for lambda suppport.

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    // This has to match compileSdkVersion above.
    implementation 'com.android.support:appcompat-v7:28.0.0'
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
}

// The sudoku Puzzle create etc code is a separate scala project.

dependencies {
    implementation project(':scala')
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:deprecation"
            // options.compilerArgs << "-Xlint:unchecked"
        }
    }
}
