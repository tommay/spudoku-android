// How to configure different scala versions, etc:
// http://jesseyates.com/2015/06/18/dev-tip-using-gradle-without-hating-it.html

apply plugin: 'scala'

repositories {
    jcenter()
}

// I updated scala from 2.11.8 to 2.11.12 (the latest 2.11, from 12/2017)
// and everything built and worked fine.  Updating to 2.12.8 built fine
// but got a java veriier error at runtime.  2.13.0-M5 (from 8/2018) had
// ProGuard errors I couldn't get rid of.  So 2.11.12 is staying.

dependencies {
    compile 'org.scala-lang:scala-library:2.11.12'
}

compileScala {
    source = "external/src/main/scala"
    scalaCompileOptions.optimize = true
    scalaCompileOptions.additionalParameters = [
        '-target:jvm-1.8'
    ]
}

// http://stackoverflow.com/questions/27494428/running-a-jar-produce-from-gradle-build
jar {
    manifest {
        attributes 'Main-Class': 'net.tommay.sudoku.Create'
    }
}

task uberjar(type: Jar) {
    appendix = "uber"
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
        configurations.runtime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
}

// Trying to figure out how to compile just one files.

task compileOne (type: ScalaCompile) {
    source = sourceSets.main.scala.srcDirs
    include 'net/tommay/sudoku/Heuristic.scala'
    classpath = sourceSets.main.compileClasspath
    destinationDir = sourceSets.main.output.classesDir
}

compileOne.options.compilerArgs = ["-sourcepath", "$projectDir/src/main/scala"]

// Delete anything we may have built while in "external".
clean {
    delete "external/build"
}
